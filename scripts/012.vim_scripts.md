

## What’s Your Favorite Color (Scheme)?

Vim ships with 17 customized color schemes. You can choose and activate a color scheme by putting the `colorscheme` command in your `.vimrc` or `.gvimrc` file.

```
colorscheme desert
```

### Conditional Execution

`.vimrc`中的if...else语句
```
if cond expr
  line of vim code
  another line of vim code
  ...
elseif some secondary cond expr
  code for this case
else
  code that runs if none of the cases apply
endif
```

Vim also allows the more terse and C-like construct:
```
cond ? expr 1 : expr 2
```

#### Using the `strftime()` function

Vim has built-in functions.

`strftime` accepts **two parameters**, the **first** of which defines **the output format of a time string**. (This format is system dependent, and not portable, so you must pay due care when choosing a format. Fortunately, most mainstream formats are common across systems.) **The second optional parameter** is **a time** measured in seconds since Jan 1, 1970 (standard C time representation). **This optional parameter** defaults to **the current time**. 

第1版本：根据时间变更vim的颜色主题

```
" Note: addition of zero, this gurantees return from function is numberic
if strftime("%H") < 6 + 0 
    colorscheme darkblue
    echo "setting colorscheme to darkblue"
elseif strftime("%H") < 12 + 0 
    colorscheme morning
    echo "setting colorscheme to morning"
elseif strftime("%H") < 18 + 0 
    colorscheme shine
    echo "setting colorscheme to shine"
else
    colorscheme evening
    echo "setting colorscheme to evening"
endif
```

其中，另一个vim script command就是`echo`。

### Variables

**Vim variables** are fairly straightforward, but there are a few things to know and manage. Specifically,  we  must  manage  our  **variable’s  scope**.  Vim defines **a variable’s scope** through a convention that depends on **the name’s prefix**. The prefixes include:

```
b:    # A variable recognized in a single Vim buffer
w:    # A variable recognized in a single Vim window
t:    # A variable recognized in a single Vim tab
g:    # A variable recognized globally—i.e., it can be referenced anywhere
l:    # A variable recognized within the function (a local variable)
s:    # A variable recognized within the sourced Vim script
a:    # A function argument
v:    # A Vim variable—one controlled by Vim (these are also global variables)
```

If you do not define **a Vim variable’s scope** with a prefix, it defaults to **a global (`g:`) variable** when defined **outside a function**, and to **a local (`l:`) variable** when defined **within a function**.

You assign **a value** to **a variable** with the `let` command:
```
:let var = "value"
```

第2版本：根据时间变更vim的颜色主题
```
" Note: addition of zero, this gurantees return from function is numberic
let currentHour = strftime("%H")
echo "currentHour is " . currentHour
if currentHour < 6 + 0
    let colorScheme = "darkblue"
elseif currentHour < 12 + 0
    let colorScheme = "morning"
elseif currentHour < 18 + 0
    let colorScheme = "shine"
else
    let colorScheme = "evening"
endif
echo "setting colorscheme to " . colorScheme
execute "colorscheme " . colorScheme
```

代码讲解：`echo "currentHour is " . currentHour`

Notice the use of **the dot (`.`) notation** with the `echo` command. This operator concatenates **expressions** into **one string**, which `echo` ultimately displays.

代码讲解：`execute "colorscheme " . colorScheme`

We need a way to execute a Vim command that refers to **a variable** instead of **a literal string** such as *darkblue*. Vim gives us the `execute` command for this purpose. When passed a command, it evaluates **variables** and **expressions** and substitutes **their values** into the command. 

另外一种验证的方法就是在vim启动之后，输入以下命令查看当前的colorscheme：
```
:echo colors_name
```

### Defining Functions

Vim lets you define your own functions with `function...endfunction` statements. Here is a sample skeleton of a user-defined function:
```
function myFunction (arg1, arg2...)
  line of code
  another line of code
endfunction
```

第3版本：根据时间变更vim的颜色主题
```
function SetTimeOfDayColors()
    " Note: addition of zero, this gurantees return from function is numberic
    let currentHour = strftime("%H")
    echo "currentHour is " . currentHour
    if currentHour < 6 + 0
        let colorScheme = "darkblue"
    elseif currentHour < 12 + 0
        let colorScheme = "morning"
    elseif currentHour < 18 + 0
        let colorScheme = "shine"
    else
        let colorScheme = "evening"
    endif
    echo "setting color to " . colorScheme
    execute "colorscheme " . colorScheme
endfunction
```

**Vim user-defined function names** must begin with **a capital letter**.

> 注意：vim中用户自定义的function的名字首字母必须“大写”。

当然，这里只是定义了这个function，并没有调用，可以在`.vimrc`文件中，或启动vim之后，输入如下命令：

```
call SetTimeOfDayColors()
```

### Arrays

**Vim arrays** are created by defining a variable’s value as **a comma-separated list of values** within **square brackets**. 

第4版本：根据时间变更vim的颜色主题
```
let g:colors_name = "default"
let g:Favcolorschemes = ["darkblue", "morning", "shine", "evening"]
function SetTimeOfDayColors()
	" currentHour will be 0, 1, 2, or 3
	let g:CurrentHour = (strftime("%H") + 0) / 6
	if g:colors_name !~ g:Favcolorschemes[g:CurrentHour]
		execute "colorscheme " . g:Favcolorschemes[g:CurrentHour]
		echo "execute colorscheme " . g:Favcolorschemes[g:CurrentHour]
	endif
endfunction

call SetTimeOfDayColors()
```

## Dynamic File Type Configuration Through Scripting

Normally, when editing **a new file**, the only clue `Vim` gets in order to determine and set `filetype` is **the file’s extension**.

> 正常情况下，是通过文件的扩展名来决定filetype。

But not all files require an extension. Vim is actually sufficiently well-trained to recognize a shell script without the crutch of **a file extension**, by looking at **the text** inside the file. However, it can do so only on the second edit, when the file provides some context for determining the type. Vim scripts can fix that!

> vim可以通过文件的内容来决定filetype。

### Autocommands

**Autocommands** include **any valid Vim commands**. Vim uses **events** to execute **commands**. Some examples of **Vim events** include:

```
BufNewFile    # Triggers an associated command when Vim begins editing a new file
BufReadPre    # Triggers an associated command before Vim moves to a new buffer
BufRead, BufReadPost    # Trigger an associated command when editing a new buffer, but after reading the file
BufWrite, BufWritePre    # Trigger an associated command before writing a buffer to a file
FileType      # Triggers an associated command after setting the filetype
VimResized    # Triggers an associated command after a Vim window size has changed
WinEnter, WinLeave    # Trigger  an  associated  command  upon  entering  or  leaving  a  Vim  window, respectively
CursorMoved, CursorMovedI    # Trigger an associated command every time the cursor moves in normal mode or in insert mode, respectively
```

Altogether there are almost **80 Vim events**. For any of these events, you can define an **automatic autocmd** that executes when that event occurs. The `autocmd` format is:
```
autocmd [group] event pattern [nested] command
```

The elements of this format are:
- `group` An optional command group 
- `event` The event that will trigger command
- `pattern` The **pattern** matching **the filename** for which command should execute
- `nested` If present, allows this autocommand to be nested within others
- `command` The Vim command, function, or user-defined script to execute when the event occurs



```
autocmd CursorMovedI * call CheckFileType()

function CheckFileType()
    if exists("b:countCheck") == 0
        let b:countCheck = 0
    endif

    let b:countCheck += 1

    " Don't start detecting until approx. 20 char
    if &filetype == "" && b:countCheck > 20 && b:countCheck < 200
        filetype detect
    endif
endfunction
```

Vim scripts use **special variables** to extract **values** from **options**, by prefixing **the option name** (`filetype` in our case) with **an ampersand (`&`) character**. Hence we will use the variable `&filetype` in our function.

A  quick  check  with  `set filetype` verifies that Vim has made the correct assignment.

> 这里主要是讲`&filetype`变量，在vim中，也可以通过`:echo &filetype`查看该变量的值，也可以通过`:set filetype`进行查看。

The Vim command `filetype detect` is a **Vim script** installed in the `$VIMRUNTIME` directory. It runs through many criteria and tries to assign **a file type** to **your file**. Normally **this occurs once**, so if **the file is new** and **filetype cannot determine a file type**, **the edit session** cannot assign **syntax formatting**.


### Autocommands and Groups

We need a way to stop calling code when we don’t need it. For this we leverage Vim’s notion of **autocommand groups** and their ability to remove commands based on their **group association**.

Vim provides an `augroup` command to do this. Its syntax is:
```vim
augroup groupname
```
All subsequent `autocmd` definitions become associated with group groupname until the statement:
```vim
augroup END
```

Vim lets you delete an **autocommand** simply by referencing **the event**, **the pattern** that filenames must match, or **its group**.
```vim
autocmd! [group] [event] [pattern]
```
The usual Vim “**force**” character—an exclamation point (`!`)—follows the `autocmd` keyword to indicate that **commands** associated with **the group**, **event**, or **pattern** are to be removed.

```vim
augroup newFileDetection
autocmd CursorMovedI * call CheckFileType()
augroup END

function CheckFileType()
    if exists("b:countCheck") == 0
        let b:countCheck = 0
    endif

    let b:countCheck += 1

    " Don't start detecting until approx. 20 char
    if &filetype == "" && b:countCheck > 20 && b:countCheck < 200
        filetype detect
    elseif b:countCheck >= 200 || &filetype != ""
        autocmd! newFileDetection
    endif
endfunction
```

## A Useful Vim Script Example

```
autocmd BufWritePre,FileWritePre *.html mark s|call LastMod()|'s
fun LastMod()
    " if there are more than 20 lines, set max to 20,
    " otherwise, scan entire file
    if line("$") > 20
        let lastModifiedline = 20
    else
        let lastModifiedline = line("$")
    endif
    exe  "1," . lastModifiedline . "g/Last modified: /s/Last modified:.*/Last modified: " . strftime("%Y-%m-%d %T")
endfun
```

在vim中，可以通过`:help strftime`查看时间格式的帮助文档。


## Resources

A good starting point is to go to the source of Vim itself and visit the pages specifically dedicated to scripting: 
https://www.vim.org/scripts/index.php

We also remind you that the built-in Vim help is invaluable. The most productive help topics we recommend are:
```
help autocmd
help scripts
help variables
help functions
help usr_41.txt
```

